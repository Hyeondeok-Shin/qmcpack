# Add the directory of the first library file to RPATH/RUNPATH if that directory is not part of OS default locations.
function(AddRPATH LIBRARY_NAME LIBRARY_FILE)
  if(LIBRARY_FILE)
    cmake_path(GET LIBRARY_FILE PARENT_PATH LIBRARY_DIR)
    list(FIND CMAKE_PLATFORM_IMPLICIT_LINK_DIRECTORIES ${LIBRARY_DIR} isSystemDir)
    if("${isSystemDir}" STREQUAL "-1")
      message(STATUS "Append ${LIBRARY_NAME} runtime library path ${LIBRARY_DIR}")
      list(APPEND CMAKE_BUILD_RPATH ${LIBRARY_DIR})
      list(APPEND CMAKE_INSTALL_RPATH ${LIBRARY_DIR})
      set(CMAKE_BUILD_RPATH "${CMAKE_BUILD_RPATH}" PARENT_SCOPE)
      set(CMAKE_INSTALL_RPATH "${CMAKE_INSTALL_RPATH}" PARENT_SCOPE)
    endif()
  endif()
endfunction()
